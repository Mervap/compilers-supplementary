-- Expression evaluator

import Data;
import Expr;
import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
fun evalOp(op, l, r) {
  case assoc(ops, op) of
    Some (f) -> f(l, r)
  esac
}

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList(c, exprs) {
  case foldl(
    fun ([c, vals], e) {
      case eval(c, e) of
        [c, v] -> [c, v:vals]
      esac
    },
    [c, {}],
    exprs
  )
  of
    [c, vals] -> [c, reverse(vals)]
  esac
}

fun matches(pattern, v) {
  case pattern of
      Wildcard   -> true
    | Named (_, None) -> true
    | Named (_, pattern) -> pattern.matches(v)
    | Const (n)  ->
      case v of
        #val -> n == v
        | _ -> false
      esac
    | String (s) ->
      case v of
        #str -> compare(s, v) == 0
        | _ -> false
      esac
    | Array (a) ->
      case v of
        #array ->
          if a.length == v.length
            then
              var ans = true, ind = 0;
              while (ans && ind < a.length) do
                ans := ans && a[ind].matches(v[ind]);
                ind := ind + 1
              od;
              ans
            else false
          fi
        | _ -> false
      esac
    | Sexp (name, data) ->
      case v of
        Sexp(vName, vData) ->
          if name === vName
            then Array(listArray(data)).matches(vData)
            else false
          fi
        | _ -> false
      esac
  esac
}

fun evalCase(c, scrutinee, branches, loc) {
  case branches of
    {} -> failure("Failed to match %s at %d:%d\n", s.scrutinee, loc.fst, loc.snd)
    | [pattern, expr] : xs ->
      if pattern.matches(scrutinee)
        then eval(c, expr)
        else evalCase(c, scrutinee, xs, loc)
      fi
  esac
}

fun eval(c@[st, world], expr) {
  case expr of
    Assn (ref, expr) ->
      case evalList(c, { expr, ref }) of
          [c, { value, ElemRef(addr, i) }] -> [c, addr[i] := value]
        | [[st, world], { value, name }] -> [[st <- [name, value], world], value]
      esac
    | Set (to, from) -> eval(c, Assn(Ref(to), from))
    | Seq  (stmt1, stmt2) ->
      case evalList(c, { stmt1, stmt2 }) of
        [c, { _, v }] -> [c, v]
      esac
    | Skip -> [c, {}]
    | If (condition, thenBranch, elseBranch) ->
      case eval(c, condition) of
          [c, 0] -> eval(c, elseBranch)
        | [c, _] -> eval(c, thenBranch)
      esac
    | While (condition, body) ->
      var conf = c;
      while
        case eval(conf, condition) of
          [c, v] -> conf := c; v
        esac
      do
        conf := eval(conf, body)[0]
      od;
      [conf, Void]
    | DoWhile(body, condition) ->
      var conf = c;
      do
        conf := eval(conf, body)[0]
      while
        case eval(conf, condition) of
          [c, v] -> conf := c; v
        esac
      od;
      [conf, Void]
    | Var (name)  -> [c, st.lookup(name)]
    | Ref (name)  -> [c, name]
    | Const (int) -> [c, int]
    | Binop (opName, l, r) ->
      case evalList(c, { l, r }) of
        [c, { lv, rv }] -> [c, evalOp(opName, lv, rv)]
      esac
    | Ignore (e) -> [eval(c, e)[0], Void]
    | Scope (defs, e)  ->
      case eval([st.enterScope.withDefs(defs), world], e) of
        [[newSt, newWorld], v] -> [[newSt.leaveScope, newWorld], v]
      esac
    | Call (name, args) ->
      var callSt = st;
      case st.lookup(name) of
        Fun(names, body) ->
          case evalList(c, args) of
            [[st, world], values] ->
              case body of
                External ->
                  case evalBuiltin(name, values, world) of
                    [v, newWorld] -> [[st, newWorld], v]
                  esac
                | _      ->
                  case eval([st.enterFunction.withArgs(names, values), world], body) of
                    [[st, world], v] -> [[callSt.leaveFunction(st.getGlobal), world], v]
                  esac
              esac
          esac
      esac
    | String (s)  -> [c, s]
    | Array (arr) ->
      case evalList(c, arr) of
        [c, arr] -> [c, listArray(arr)]
      esac
    | Elem (arr, i) ->
      case evalList(c, { arr, i }) of
        [c, arr : i : {}] ->
          case arr of
              Sexp (_, data) -> [c, data[i]]
            | arr            -> [c, arr[i]]
          esac
      esac
    | Sexp (name, data) ->
      case evalList(c, data) of
        [c, data] -> [c, Sexp(name, listArray(data))]
      esac
    | ElemRef (arr, i) ->
      case evalList(c, { arr, i }) of
        [c, arr : i : {}] ->
          case arr of
              Sexp (_, data) -> [c, ElemRef(data, i)]
            | arr            -> [c, ElemRef(arr, i)]
          esac
      esac
    | Case (loc, name, branches) ->
      case st.lookup(name) of
        v -> evalCase(c, v, branches, loc)
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr(input, expr) {
  case
    eval(
      [
        emptyState().enterScope
          .addName("read", Fun({}, External))
          .addName("write", Fun({ "a" }, External))
          .addName("length", Fun({ "a" }, External)),
        createWorld(input)
      ],
      expr)
  of
    [c, _] -> c.snd.getOutput
  esac
}
